use bumpalo::Bump;
use panko_lex::Token;

use crate as ast;

grammar<'a>(bump: &'a Bump);

extern {
    type Error = ast::Error<'a>;

    enum Token<'a> {
        ";" => Token { kind: panko_lex::TokenKind::Semicolon, .. },
        "," => Token { kind: panko_lex::TokenKind::Comma, .. },
        "=" => Token { kind: panko_lex::TokenKind::Equal, .. },

        "*" => Token { kind: panko_lex::TokenKind::Star, .. },

        // parentheses
        "(" => Token { kind: panko_lex::TokenKind::LParen, .. },
        ")" => Token { kind: panko_lex::TokenKind::RParen, .. },
        "{" => Token { kind: panko_lex::TokenKind::LBrace, .. },
        "}" => Token { kind: panko_lex::TokenKind::RBrace, .. },

        // StorageClassSpecifierToken
        "auto" => Token { kind: panko_lex::TokenKind::Auto, .. },
        "constexpr" => Token { kind: panko_lex::TokenKind::Constexpr, .. },
        "extern" => Token { kind: panko_lex::TokenKind::Extern, .. },
        "register" => Token { kind: panko_lex::TokenKind::Register, .. },
        "static" => Token { kind: panko_lex::TokenKind::Static, .. },
        "thread_local" => Token { kind: panko_lex::TokenKind::ThreadLocal, .. },
        "typedef" => Token { kind: panko_lex::TokenKind::Typedef, .. },

        // TypeSpecifierToken
        "void" => Token { kind: panko_lex::TokenKind::Void, .. },
        "char" => Token { kind: panko_lex::TokenKind::Char, .. },
        "short" => Token { kind: panko_lex::TokenKind::Short, .. },
        "int" => Token { kind: panko_lex::TokenKind::Int, .. },
        "long" => Token { kind: panko_lex::TokenKind::Long, .. },
        "float" => Token { kind: panko_lex::TokenKind::Float, .. },
        "double" => Token { kind: panko_lex::TokenKind::Double, .. },
        "signed" => Token { kind: panko_lex::TokenKind::Signed, .. },
        "unsigned" => Token { kind: panko_lex::TokenKind::Unsigned, .. },
        "bool" => Token { kind: panko_lex::TokenKind::Bool, .. },
        "_Complex" => Token { kind: panko_lex::TokenKind::Complex, .. },
        "_Decimal32" => Token { kind: panko_lex::TokenKind::Decimal32, .. },
        "_Decimal64" => Token { kind: panko_lex::TokenKind::Decimal64, .. },
        "_Decimal128" => Token { kind: panko_lex::TokenKind::Decimal128, .. },

        // TypeQualifierToken
        "const" => Token { kind: panko_lex::TokenKind::Const, .. },
        "restrict" => Token { kind: panko_lex::TokenKind::Restrict, .. },
        "volatile" => Token { kind: panko_lex::TokenKind::Volatile, .. },
        "_Atomic" => Token { kind: panko_lex::TokenKind::Atomic, .. },

        // FunctionSpecifierToken
        "inline" => Token { kind: panko_lex::TokenKind::Inline, .. },
        "_Noreturn" => Token { kind: panko_lex::TokenKind::Noreturn, .. },

        "return" => Token { kind: panko_lex::TokenKind::Return, .. },

        "+" => Token { kind: panko_lex::TokenKind::Plus, .. },
        "-" => Token { kind: panko_lex::TokenKind::Minus, .. },

        "==" => Token { kind: panko_lex::TokenKind::EqualEqual, .. },
        "!=" => Token { kind: panko_lex::TokenKind::BangEqual, .. },

        "<" => Token { kind: panko_lex::TokenKind::Less, .. },
        "<=" => Token { kind: panko_lex::TokenKind::LessEqual, .. },
        ">" => Token { kind: panko_lex::TokenKind::Greater, .. },
        ">=" => Token { kind: panko_lex::TokenKind::GreaterEqual, .. },

        "&" => Token { kind: panko_lex::TokenKind::And, .. },

        "..." => Token { kind: panko_lex::TokenKind::Ellipsis, .. },

        Identifier => Token { kind: panko_lex::TokenKind::Identifier, .. },
        Integer => Token { kind: panko_lex::TokenKind::Integer, .. },
    }
}

pub TranslationUnit: ast::TranslationUnit<'a> = {
    Many<ExternalDeclaration> => ast::TranslationUnit { decls: <> },
}

ExternalDeclaration: ast::ExternalDeclaration<'a> = {
    FunctionDefinition => ast::ExternalDeclaration::FunctionDefinition(<>),
    Declaration => ast::ExternalDeclaration::Declaration(<>),
}

FunctionDefinition: ast::FunctionDefinition<'a> = {
    AttributeSpecifierSequence?
        <declaration_specifiers:DeclarationSpecifiers>
        <declarator:Declarator>
        <body:FunctionBody> => ast::FunctionDefinition { <> },
}

FunctionBody = { CompoundStatement }

Declaration: ast::Declaration<'a> = {
    <specifiers:DeclarationSpecifiers> <init_declarator_list:InitDeclaratorList> ";" =>
        ast::Declaration { <> },
    // attribute-specifier-sequence declaration-specifiers init_declarator_list ";"
    // static_assert-declaration
    // attribute-declaration
}

DeclarationSpecifiers: ast::DeclarationSpecifiers<'a> = {
    Some<(<DeclarationSpecifier> AttributeSpecifierSequence?)> => ast::DeclarationSpecifiers(<>),
}

DeclarationSpecifier: ast::DeclarationSpecifier<'a> = {
    StorageClassSpecifier => ast::DeclarationSpecifier::StorageClass(<>),
    TypeSpecifierQualifier => ast::DeclarationSpecifier::TypeSpecifierQualifier(<>),
    FunctionSpecifier => ast::DeclarationSpecifier::FunctionSpecifier(<>),
}

InitDeclaratorList: &'a [ast::InitDeclarator<'a>] = {
    CommaSep<InitDeclarator>,
}

InitDeclarator: ast::InitDeclarator<'a> = {
    <declarator:Declarator> <initialiser:("=" <Initialiser>)?> => ast::InitDeclarator { <> },
}

Declarator: ast::Declarator<'a> = {
    <pointers:Some<Pointer>?> <direct_declarator:DirectDeclarator> => ast::Declarator { <> },
}

Pointer: ast::Pointer<'a> = {
    <star:"*"> AttributeSpecifierSequence? <qualifiers:Many<TypeQualifier>> => ast::Pointer { <> },
}

DirectDeclarator: ast::DirectDeclarator<'a> = {
    <Identifier> AttributeSpecifierSequence? => ast::DirectDeclarator::Identifier(<>),
    "(" <Bump<Declarator>> ")" => ast::DirectDeclarator::Parenthesised(<>),
    // array-declarator attribute-specifier-sequence?
    <FunctionDeclarator> AttributeSpecifierSequence? => ast::DirectDeclarator::FunctionDeclarator(<>),
}

FunctionDeclarator: ast::FunctionDeclarator<'a> = {
    <direct_declarator:Bump<DirectDeclarator>> "(" <parameter_type_list:ParameterTypeList> <close_paren:")"> =>
        ast::FunctionDeclarator { <> },
}

ParameterTypeList: ast::ParameterTypeList<'a> = {
    () => ast::ParameterTypeList { parameter_list: &[], is_varargs: false },
    <parameter_list:NonEmptyCommaSep<ParameterDeclaration>> <is_varargs:("," "...")?> => {
        let is_varargs = is_varargs.is_some();
        ast::ParameterTypeList { <> }
    },
    "..." => ast::ParameterTypeList { parameter_list: &[], is_varargs: true },
}

ParameterDeclaration: ast::ParameterDeclaration<'a> = {
    AttributeSpecifierSequence? <declaration_specifiers:DeclarationSpecifiers>
        <declarator:ParameterDeclarator> => ast::ParameterDeclaration { <> },
}

ParameterDeclarator: Option<ast::Declarator<'a>> = {
    Declarator => Some(<>),
    AbstractDeclarator?,
}

AbstractDeclarator: ast::Declarator<'a> = {
    Some<Pointer> => ast::Declarator { pointers: Some(<>), direct_declarator: ast::DirectDeclarator::Abstract },
    <pointers:Some<Pointer>?> <direct_declarator:DirectAbstractDeclarator>
        => ast::Declarator { <> },
}

DirectAbstractDeclarator: ast::DirectDeclarator<'a> = {
    "(" <Bump<AbstractDeclarator>> ")" => ast::DirectDeclarator::Parenthesised(<>),
    // array-abstract-declarator attribute-specifier-sequence?
    <FunctionAbstractDeclarator> AttributeSpecifierSequence?
        => ast::DirectDeclarator::FunctionDeclarator(<>),
}

FunctionAbstractDeclarator: ast::FunctionDeclarator<'a> = {
    <direct_declarator:Bump<DirectAbstractDeclarator>?>
        "(" <parameter_type_list:ParameterTypeList> <close_paren:")"> => {
            let direct_declarator = direct_declarator.unwrap_or(&ast::DirectDeclarator::Abstract);
            ast::FunctionDeclarator { <> }
        },
}

Initialiser: ast::Expression<'a> = {
    // TODO: incomplete: `braced-initializer` is missing
    AssignmentExpression
}

AttributeSpecifierSequence: () = {}

StorageClassSpecifier: ast::StorageClassSpecifier<'a> = {
    StorageClassSpecifierToken => ast::StorageClassSpecifier::new(<>),
}

StorageClassSpecifierToken = {
    "auto",
    "constexpr",
    "extern",
    "register",
    "static",
    "thread_local",
    "typedef",
}

TypeSpecifierQualifier: ast::TypeSpecifierQualifier<'a> = {
    TypeSpecifier => ast::TypeSpecifierQualifier::Specifier(<>),
    TypeQualifier => ast::TypeSpecifierQualifier::Qualifier(<>),
}

TypeSpecifier: ast::TypeSpecifier<'a> = {
    TypeSpecifierToken => ast::TypeSpecifier::new(<>),
    BitIntTypeSpecifier => todo!(),
    AtomicTypeSpecifier => todo!(),
    StructOrUnionSpecifier => todo!(),
    EnumSpecifier => todo!(),
    TypedefName => todo!(),
    TypeofSpecifier => todo!(),
}

TypeSpecifierToken = {
    "void",
    "char",
    "short",
    "int",
    "long",
    "float",
    "double",
    "signed",
    "unsigned",
    "bool",
    "_Complex",
    "_Decimal32",
    "_Decimal64",
    "_Decimal128",
}

BitIntTypeSpecifier: () = {}
AtomicTypeSpecifier: () = {}
StructOrUnionSpecifier: () = {}
EnumSpecifier: () = {}
TypedefName: () = {}
TypeofSpecifier: () = {}

TypeQualifier: ast::TypeQualifier<'a> = {
    TypeQualifierToken => ast::TypeQualifier::new(<>),
}

TypeQualifierToken = {
    "const",
    "restrict",
    "volatile",
    "_Atomic",
}

FunctionSpecifier: ast::FunctionSpecifier<'a> = {
    FunctionSpecifierToken => ast::FunctionSpecifier::new(<>),
}

FunctionSpecifierToken = {
    "inline",
    "_Noreturn",
}

CompoundStatement: ast::CompoundStatement<'a> = {
    "{" <BlockItemList?> "}" => ast::CompoundStatement(<>.unwrap_or(&[])),
}

BlockItemList: &'a [ast::BlockItem<'a>] = {
    Some<BlockItem>,
}

BlockItem: ast::BlockItem<'a> = {
    Declaration => ast::BlockItem::Declaration(<>),
    UnlabeledStatement => ast::BlockItem::UnlabeledStatement(<>),
    // label
}

UnlabeledStatement: ast::UnlabeledStatement<'a> = {
    ExpressionStatement => ast::UnlabeledStatement::ExpressionStatement(<>),
    AttributeSpecifierSequence? <PrimaryBlock> => ast::UnlabeledStatement::PrimaryBlock(<>),
    AttributeSpecifierSequence? <JumpStatement> => ast::UnlabeledStatement::JumpStatement(<>),
}

PrimaryBlock: ast::PrimaryBlock<'a> = {
    CompoundStatement => ast::PrimaryBlock::CompoundStatement(<>),
    // selection-statement
    // iteration-statement
}

JumpStatement: ast::JumpStatement<'a> = {
    // goto Identifier ;
    // continue ;
    // break ;
    <return_:"return"> <expr:Expression?> ";" => ast::JumpStatement::Return { <> },
}

ExpressionStatement: ast::ExpressionStatement<'a> = {
    <Expression?> ";" => ast::ExpressionStatement(<>),
    // FIXME: this produces a conflict
    // AttributeSpecifierSequence? <Expression> ";" => ast::ExpressionStatement(Some(<>)),
}

PrimaryExpression: ast::Expression<'a> = {
    Identifier => ast::Expression::Name(<>),
    Integer => ast::Expression::Integer(<>),
    <open_paren:"("> <expr:Bump<Expression>> <close_paren:")"> => ast::Expression::Parenthesised { <> },
}

PostfixExpression: ast::Expression<'a> = {
    PrimaryExpression,
    <callee:Bump<PostfixExpression>> "(" <args:ArgumentExpressionList> <close_paren:")"> =>
        ast::Expression::Call { <> },
}

ArgumentExpressionList = { CommaSep<AssignmentExpression> }

UnaryOperator: ast::UnaryOp<'a> = {
    "&" => ast::UnaryOp { kind: ast::UnaryOpKind::Addressof, token: <> },
    "*" => ast::UnaryOp { kind: ast::UnaryOpKind::Deref, token: <> },
    "-" => ast::UnaryOp { kind: ast::UnaryOpKind::Negate, token: <> },
}

UnaryExpression: ast::Expression<'a> = {
    PostfixExpression,
    <operator:UnaryOperator> <operand:Bump<CastExpression>> => ast::Expression::UnaryOp { <> },
}

CastExpression: ast::Expression<'a> = {
    UnaryExpression,
}

MultiplicativeExpression: ast::Expression<'a> = {
    CastExpression,
}

AdditiveOperator: ast::BinOpKind = {
    "+" => ast::BinOpKind::Add,
    "-" => ast::BinOpKind::Subtract,
}

AdditiveExpression: ast::Expression<'a> = {
    MultiplicativeExpression,
    <lhs:Bump<AdditiveExpression>> <kind:AdditiveOperator> <rhs:Bump<MultiplicativeExpression>> =>
        ast::Expression::BinOp { <> },
}

ShiftExpression: ast::Expression<'a> = {
    AdditiveExpression,
}

RelationalOperator: ast::BinOpKind = {
    "<" => ast::BinOpKind::Less,
    "<=" => ast::BinOpKind::LessEqual,
    ">" => ast::BinOpKind::Greater,
    ">=" => ast::BinOpKind::GreaterEqual,
}

RelationalExpression: ast::Expression<'a> = {
    ShiftExpression,
    <lhs:Bump<RelationalExpression>> <kind:RelationalOperator> <rhs:Bump<ShiftExpression>> =>
        ast::Expression::BinOp { <> },
}

EqualityOperator: ast::BinOpKind = {
    "==" => ast::BinOpKind::Equal,
    "!=" => ast::BinOpKind::NotEqual,
}

EqualityExpression: ast::Expression<'a> = {
    RelationalExpression,
    <lhs:Bump<EqualityExpression>> <kind:EqualityOperator> <rhs:Bump<RelationalExpression>> =>
        ast::Expression::BinOp { <> },
}

ConditionalExpression: ast::Expression<'a> = {
    EqualityExpression,
}

AssignmentExpression: ast::Expression<'a> = {
    ConditionalExpression,
    <target:Bump<UnaryExpression>> "=" <value:Bump<AssignmentExpression>> => ast::Expression::Assign { <> },
}

Expression: ast::Expression<'a> = {
    AssignmentExpression,
}

Bump<T>: &'a T = {
    T => bump.alloc(<>),
}

Some<T>: &'a [T] = {
    T+ => bump.alloc_slice_copy(&<>),
}

Many<T>: &'a [T] = {
    T* => bump.alloc_slice_copy(&<>),
}

CommaSep<T>: &'a [T] = {
    () => &[],
    NonEmptyCommaSep<T>,
}

#[inline]
NonEmptyCommaSep<T>: &'a [T] = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        bump.alloc_slice_copy(&v)
    },
}
