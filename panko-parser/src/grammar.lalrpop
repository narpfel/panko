use panko_lex::Token;

use crate as ast;
use crate::ast::Session;

grammar<'a>(sess: &'a Session<'a>);

extern {
    type Error = ast::Error<'a>;

    enum Token<'a> {
        ";" => Token { kind: panko_lex::TokenKind::Semicolon, .. },
        "," => Token { kind: panko_lex::TokenKind::Comma, .. },
        "=" => Token { kind: panko_lex::TokenKind::Equal, .. },

        "*" => Token { kind: panko_lex::TokenKind::Star, .. },

        // parentheses
        "(" => Token { kind: panko_lex::TokenKind::LParen, .. },
        ")" => Token { kind: panko_lex::TokenKind::RParen, .. },
        "{" => Token { kind: panko_lex::TokenKind::LBrace, .. },
        "}" => Token { kind: panko_lex::TokenKind::RBrace, .. },
        "[" => Token { kind: panko_lex::TokenKind::LBracket, .. },
        "]" => Token { kind: panko_lex::TokenKind::RBracket, .. },

        "_Generic" => Token { kind: panko_lex::TokenKind::Generic, .. },
        "default" => Token { kind: panko_lex::TokenKind::Default, .. },
        ":" => Token { kind: panko_lex::TokenKind::Colon, .. },

        // StorageClassSpecifierToken
        "auto" => Token { kind: panko_lex::TokenKind::Auto, .. },
        "constexpr" => Token { kind: panko_lex::TokenKind::Constexpr, .. },
        "extern" => Token { kind: panko_lex::TokenKind::Extern, .. },
        "register" => Token { kind: panko_lex::TokenKind::Register, .. },
        "static" => Token { kind: panko_lex::TokenKind::Static, .. },
        "thread_local" => Token { kind: panko_lex::TokenKind::ThreadLocal, .. },
        "typedef" => Token { kind: panko_lex::TokenKind::Typedef, .. },

        // TypeSpecifierToken
        "void" => Token { kind: panko_lex::TokenKind::Void, .. },
        "char" => Token { kind: panko_lex::TokenKind::Char, .. },
        "short" => Token { kind: panko_lex::TokenKind::Short, .. },
        "int" => Token { kind: panko_lex::TokenKind::Int, .. },
        "long" => Token { kind: panko_lex::TokenKind::Long, .. },
        "float" => Token { kind: panko_lex::TokenKind::Float, .. },
        "double" => Token { kind: panko_lex::TokenKind::Double, .. },
        "signed" => Token { kind: panko_lex::TokenKind::Signed, .. },
        "unsigned" => Token { kind: panko_lex::TokenKind::Unsigned, .. },
        "bool" => Token { kind: panko_lex::TokenKind::Bool, .. },
        "_Complex" => Token { kind: panko_lex::TokenKind::Complex, .. },
        "_Decimal32" => Token { kind: panko_lex::TokenKind::Decimal32, .. },
        "_Decimal64" => Token { kind: panko_lex::TokenKind::Decimal64, .. },
        "_Decimal128" => Token { kind: panko_lex::TokenKind::Decimal128, .. },

        // TypeQualifierToken
        "const" => Token { kind: panko_lex::TokenKind::Const, .. },
        "restrict" => Token { kind: panko_lex::TokenKind::Restrict, .. },
        "volatile" => Token { kind: panko_lex::TokenKind::Volatile, .. },
        "_Atomic" => Token { kind: panko_lex::TokenKind::Atomic, .. },

        // FunctionSpecifierToken
        "inline" => Token { kind: panko_lex::TokenKind::Inline, .. },
        "_Noreturn" => Token { kind: panko_lex::TokenKind::Noreturn, .. },

        "return" => Token { kind: panko_lex::TokenKind::Return, .. },

        "sizeof" => Token { kind: panko_lex::TokenKind::Sizeof, .. },
        "_Lengthof" => Token { kind: panko_lex::TokenKind::Lengthof, ..},
        "alignof" => Token { kind: panko_lex::TokenKind::Alignof, .. },

        "+" => Token { kind: panko_lex::TokenKind::Plus, .. },
        "-" => Token { kind: panko_lex::TokenKind::Minus, .. },
        "~" => Token { kind: panko_lex::TokenKind::Tilde, .. },
        "!" => Token { kind: panko_lex::TokenKind::Bang, .. },

        "/" => Token { kind: panko_lex::TokenKind::Slash, .. },
        "%" => Token { kind: panko_lex::TokenKind::Percent, .. },

        "==" => Token { kind: panko_lex::TokenKind::EqualEqual, .. },
        "!=" => Token { kind: panko_lex::TokenKind::BangEqual, .. },

        "<" => Token { kind: panko_lex::TokenKind::Less, .. },
        "<=" => Token { kind: panko_lex::TokenKind::LessEqual, .. },
        ">" => Token { kind: panko_lex::TokenKind::Greater, .. },
        ">=" => Token { kind: panko_lex::TokenKind::GreaterEqual, .. },

        "&" => Token { kind: panko_lex::TokenKind::And, .. },
        "^" => Token { kind: panko_lex::TokenKind::Hat, .. },
        "|" => Token { kind: panko_lex::TokenKind::Pipe, .. },

        "<<" => Token { kind: panko_lex::TokenKind::LessLess, .. },
        ">>" => Token { kind: panko_lex::TokenKind::GreaterGreater, .. },

        "*=" => Token { kind: panko_lex::TokenKind::StarEqual, ..},
        "/=" => Token { kind: panko_lex::TokenKind::SlashEqual, ..},
        "%=" => Token { kind: panko_lex::TokenKind::PercentEqual, ..},
        "+=" => Token { kind: panko_lex::TokenKind::PlusEqual, ..},
        "-=" => Token { kind: panko_lex::TokenKind::MinusEqual, ..},
        "<<=" => Token { kind: panko_lex::TokenKind::LessLessEqual, ..},
        ">>=" => Token { kind: panko_lex::TokenKind::GreaterGreaterEqual, ..},
        "&=" => Token { kind: panko_lex::TokenKind::AndEqual, ..},
        "^=" => Token { kind: panko_lex::TokenKind::HatEqual, ..},
        "|=" => Token { kind: panko_lex::TokenKind::PipeEqual, ..},

        "&&" => Token { kind: panko_lex::TokenKind::AndAnd, .. },
        "||" => Token { kind: panko_lex::TokenKind::PipePipe, .. },

        "?" => Token { kind: panko_lex::TokenKind::QuestionMark, .. },
        ":" => Token { kind: panko_lex::TokenKind::Colon, .. },

        "++" => Token { kind: panko_lex::TokenKind::PlusPlus, .. },
        "--" => Token { kind: panko_lex::TokenKind::MinusMinus, ..},

        "." => Token { kind: panko_lex::TokenKind::Dot, .. },
        "..." => Token { kind: panko_lex::TokenKind::Ellipsis, .. },

        Identifier => Token { kind: panko_lex::TokenKind::Identifier, .. },
        Integer => Token { kind: panko_lex::TokenKind::Integer(_), .. },
        CharConstant => Token { kind: panko_lex::TokenKind::CharConstant(_), .. },
        String => Token { kind: panko_lex::TokenKind::String, .. },
    }
}

pub TranslationUnit: ast::TranslationUnit<'a> = {
    Many<ExternalDeclaration> => ast::TranslationUnit { decls: <> },
}

ExternalDeclaration: ast::ExternalDeclaration<'a> = {
    FunctionDefinition => ast::ExternalDeclaration::FunctionDefinition(<>),
    Declaration => ast::ExternalDeclaration::Declaration(<>),
}

FunctionDefinition: ast::FunctionDefinition<'a> = {
    AttributeSpecifierSequence?
        <declaration_specifiers:DeclarationSpecifiers>
        <declarator:Declarator>
        <body:FunctionBody> => ast::FunctionDefinition { <> },
}

FunctionBody = { CompoundStatement }

Declaration: ast::Declaration<'a> = {
    <specifiers:DeclarationSpecifiers> <init_declarator_list:InitDeclaratorList> ";" =>
        ast::Declaration { <> },
    // attribute-specifier-sequence declaration-specifiers init_declarator_list ";"
    // static_assert-declaration
    // attribute-declaration
}

DeclarationSpecifiers: ast::DeclarationSpecifiers<'a> = {
    Some<(<DeclarationSpecifier> AttributeSpecifierSequence?)> => ast::DeclarationSpecifiers(<>),
}

DeclarationSpecifier: ast::DeclarationSpecifier<'a> = {
    StorageClassSpecifier => ast::DeclarationSpecifier::StorageClass(<>),
    TypeSpecifierQualifier => ast::DeclarationSpecifier::TypeSpecifierQualifier(<>),
    FunctionSpecifier => ast::DeclarationSpecifier::FunctionSpecifier(<>),
}

InitDeclaratorList: &'a [ast::InitDeclarator<'a>] = {
    CommaSep<InitDeclarator>,
}

InitDeclarator: ast::InitDeclarator<'a> = {
    <declarator:Declarator> <initialiser:("=" <Initialiser>)?> => ast::InitDeclarator { <> },
}

Declarator: ast::Declarator<'a> = {
    <pointers:Some<Pointer>?> <direct_declarator:DirectDeclarator> => ast::Declarator { <> },
}

Pointer: ast::Pointer<'a> = {
    <star:"*"> AttributeSpecifierSequence? <qualifiers:Many<TypeQualifier>> => ast::Pointer { <> },
}

DirectDeclarator: ast::DirectDeclarator<'a> = {
    <Identifier> AttributeSpecifierSequence? => ast::DirectDeclarator::Identifier(<>),
    "(" <Bump<Declarator>> ")" => ast::DirectDeclarator::Parenthesised(<>),
    <ArrayDeclarator> AttributeSpecifierSequence? => ast::DirectDeclarator::ArrayDeclarator(<>),
    <FunctionDeclarator> AttributeSpecifierSequence? => ast::DirectDeclarator::FunctionDeclarator(<>),
}

ArrayDeclarator: ast::ArrayDeclarator<'a> = {
    // TODO: incomplete
    <direct_declarator:Bump<DirectDeclarator>>
        "[" <type_qualifiers:Many<TypeQualifier>> <length:AssignmentExpression?> <close_bracket:"]"> =>
            ast::ArrayDeclarator { <> },
}

FunctionDeclarator: ast::FunctionDeclarator<'a> = {
    <direct_declarator:Bump<DirectDeclarator>> "(" <parameter_type_list:ParameterTypeList> <close_paren:")"> =>
        ast::FunctionDeclarator { <> },
}

ParameterTypeList: ast::ParameterTypeList<'a> = {
    () => ast::ParameterTypeList { parameter_list: &[], is_varargs: false },
    <parameter_list:NonEmptyCommaSep<ParameterDeclaration>> <is_varargs:("," "...")?> => {
        let is_varargs = is_varargs.is_some();
        ast::ParameterTypeList { <> }
    },
    "..." => ast::ParameterTypeList { parameter_list: &[], is_varargs: true },
}

ParameterDeclaration: ast::ParameterDeclaration<'a> = {
    AttributeSpecifierSequence? <declaration_specifiers:DeclarationSpecifiers>
        <declarator:ParameterDeclarator> => ast::ParameterDeclaration { <> },
}

ParameterDeclarator: Option<ast::Declarator<'a>> = {
    Declarator => Some(<>),
    AbstractDeclarator?,
}

DeclarationSpecifierTypeSpecifierQualifier: ast::DeclarationSpecifier<'a> = {
    TypeSpecifierQualifier => ast::DeclarationSpecifier::TypeSpecifierQualifier(<>),
}

SpecifierQualifierList: ast::DeclarationSpecifiers<'a> = {
    <Some<DeclarationSpecifierTypeSpecifierQualifier>> AttributeSpecifierSequence? =>
        ast::DeclarationSpecifiers(<>),
}

TypeName: crate::ast::QualifiedType<'a> = {
    <specifier_qualifier_list:SpecifierQualifierList> <abstract_declarator:AbstractDeclarator?> => {
        let ty = crate::ast::parse_type_specifiers(sess, specifier_qualifier_list);
        match abstract_declarator {
            None => ty,
            Some(abstract_declarator) => {
                let (ty, name) = crate::ast::parse_declarator(sess, ty, abstract_declarator);
                assert!(name.is_none());
                ty
            },
        }
    },
}

// TODO: `AbstractDeclarator` basically copies the structure of `Declarator`,
// which leads to a lot of duplication. Maybe a macro can reduce this?
AbstractDeclarator: ast::Declarator<'a> = {
    Some<Pointer> => ast::Declarator { pointers: Some(<>), direct_declarator: ast::DirectDeclarator::Abstract },
    <pointers:Some<Pointer>?> <direct_declarator:DirectAbstractDeclarator>
        => ast::Declarator { <> },
}

DirectAbstractDeclarator: ast::DirectDeclarator<'a> = {
    "(" <Bump<AbstractDeclarator>> ")" => ast::DirectDeclarator::Parenthesised(<>),
    <ArrayAbstractDeclarator> AttributeSpecifierSequence? =>
        ast::DirectDeclarator::ArrayDeclarator(<>),
    <FunctionAbstractDeclarator> AttributeSpecifierSequence? =>
        ast::DirectDeclarator::FunctionDeclarator(<>),
}

ArrayAbstractDeclarator: ast::ArrayDeclarator<'a> = {
    // TODO: incomplete
    <direct_declarator:Bump<DirectAbstractDeclarator>?>
        "[" <type_qualifiers:Many<TypeQualifier>> <length:AssignmentExpression?> <close_bracket:"]"> => {
            let direct_declarator = direct_declarator.unwrap_or(&ast::DirectDeclarator::Abstract);
            ast::ArrayDeclarator { <> }
        },
}

FunctionAbstractDeclarator: ast::FunctionDeclarator<'a> = {
    <direct_declarator:Bump<DirectAbstractDeclarator>?>
        "(" <parameter_type_list:ParameterTypeList> <close_paren:")"> => {
            let direct_declarator = direct_declarator.unwrap_or(&ast::DirectDeclarator::Abstract);
            ast::FunctionDeclarator { <> }
        },
}

Initialiser: ast::Initialiser<'a> = {
    // TODO: incomplete: `braced-initializer` can be nonempty
    <open_brace:"{"> <initialiser_list:CommaSepWithTrailingComma<DesignatedInitialiser>>
        <close_brace:"}"> => ast::Initialiser::Braced { <> },
    AssignmentExpression => ast::Initialiser::Expression(<>),
}

DesignatedInitialiser: ast::DesignatedInitialiser<'a> = {
    <designation:(<Designation> "=")?> <initialiser:Bump<Initialiser>> => ast::DesignatedInitialiser { <> },
}

Designation: ast::Designation<'a> = {
    Some<Designator> => ast::Designation(<>),
}

Designator: ast::Designator<'a> = {
    <open_bracket:"["> <index:Expression> <close_bracket:"]"> => ast::Designator::Bracketed { <> },
    <dot:"."> <ident:Identifier> => ast::Designator::Identifier { <> },
}

AttributeSpecifierSequence: () = {}

StorageClassSpecifier: ast::StorageClassSpecifier<'a> = {
    StorageClassSpecifierToken => ast::StorageClassSpecifier::new(<>),
}

StorageClassSpecifierToken = {
    "auto",
    "constexpr",
    "extern",
    "register",
    "static",
    "thread_local",
    "typedef",
}

TypeSpecifierQualifier: ast::TypeSpecifierQualifier<'a> = {
    TypeSpecifier => ast::TypeSpecifierQualifier::Specifier(<>),
    TypeQualifier => ast::TypeSpecifierQualifier::Qualifier(<>),
}

TypeSpecifier: ast::TypeSpecifier<'a> = {
    TypeSpecifierToken => ast::TypeSpecifier::new(<>),
    BitIntTypeSpecifier => todo!(),
    AtomicTypeSpecifier => todo!(),
    StructOrUnionSpecifier => todo!(),
    EnumSpecifier => todo!(),
    TypedefName => todo!(),
    TypeofSpecifier => todo!(),
}

TypeSpecifierToken = {
    "void",
    "char",
    "short",
    "int",
    "long",
    "float",
    "double",
    "signed",
    "unsigned",
    "bool",
    "_Complex",
    "_Decimal32",
    "_Decimal64",
    "_Decimal128",
}

BitIntTypeSpecifier: () = {}
AtomicTypeSpecifier: () = {}
StructOrUnionSpecifier: () = {}
EnumSpecifier: () = {}
TypedefName: () = {}
TypeofSpecifier: () = {}

TypeQualifier: ast::TypeQualifier<'a> = {
    TypeQualifierToken => ast::TypeQualifier::new(<>),
}

TypeQualifierToken = {
    "const",
    "restrict",
    "volatile",
    "_Atomic",
}

FunctionSpecifier: ast::FunctionSpecifier<'a> = {
    FunctionSpecifierToken => ast::FunctionSpecifier::new(<>),
}

FunctionSpecifierToken = {
    "inline",
    "_Noreturn",
}

CompoundStatement: ast::CompoundStatement<'a> = {
    "{" <BlockItemList?> "}" => ast::CompoundStatement(<>.unwrap_or(&[])),
}

BlockItemList: &'a [ast::BlockItem<'a>] = {
    Some<BlockItem>,
}

BlockItem: ast::BlockItem<'a> = {
    Declaration => ast::BlockItem::Declaration(<>),
    UnlabeledStatement => ast::BlockItem::UnlabeledStatement(<>),
    // label
}

UnlabeledStatement: ast::UnlabeledStatement<'a> = {
    ExpressionStatement => ast::UnlabeledStatement::ExpressionStatement(<>),
    AttributeSpecifierSequence? <PrimaryBlock> => ast::UnlabeledStatement::PrimaryBlock(<>),
    AttributeSpecifierSequence? <JumpStatement> => ast::UnlabeledStatement::JumpStatement(<>),
}

PrimaryBlock: ast::PrimaryBlock<'a> = {
    CompoundStatement => ast::PrimaryBlock::CompoundStatement(<>),
    // selection-statement
    // iteration-statement
}

JumpStatement: ast::JumpStatement<'a> = {
    // goto Identifier ;
    // continue ;
    // break ;
    <return_:"return"> <expr:Expression?> ";" => ast::JumpStatement::Return { <> },
}

ExpressionStatement: ast::ExpressionStatement<'a> = {
    <Expression?> ";" => ast::ExpressionStatement(<>),
    // FIXME: this produces a conflict
    // AttributeSpecifierSequence? <Expression> ";" => ast::ExpressionStatement(Some(<>)),
}

PrimaryExpression: ast::Expression<'a> = {
    Identifier => ast::Expression::Name(<>),
    Integer => ast::Expression::Integer(<>),
    CharConstant => ast::Expression::CharConstant(<>),
    Some<String> => ast::Expression::String(<>),
    <open_paren:"("> <expr:Bump<Expression>> <close_paren:")"> => ast::Expression::Parenthesised { <> },
    <generic:"_Generic"> "("
        <selector:Bump<AssignmentExpression>> ","
        <assocs:GenericAssocList>
        <close_paren:")"> => ast::Expression::Generic { <> },
}

GenericAssocList: ast::GenericAssocList<'a> = {
    NonEmptyCommaSep<GenericAssociation> => ast::GenericAssocList(<>),
}

GenericAssociation: ast::GenericAssociation<'a> = {
    <ty:TypeName> ":" <expr:AssignmentExpression> => ast::GenericAssociation::Ty { <> },
    <default:"default"> ":" <expr:AssignmentExpression> => ast::GenericAssociation::Default { <> },
}

IncrementOperator: ast::IncrementOp<'a> = {
    "++" => ast::IncrementOp { kind: ast::IncrementOpKind::Increment, token: <> },
    "--" => ast::IncrementOp { kind: ast::IncrementOpKind::Decrement, token: <> },
}

PostfixExpression: ast::Expression<'a> = {
    PrimaryExpression,
    <callee:Bump<PostfixExpression>> "(" <args:ArgumentExpressionList> <close_paren:")"> =>
        ast::Expression::Call { <> },
    <lhs:Bump<PostfixExpression>> "[" <rhs:Bump<Expression>> <close_bracket:"]"> =>
        ast::Expression::Subscript { <> },
    <operand:Bump<PostfixExpression>> <operator:IncrementOperator> =>
        ast::Expression::Increment { <>, fixity: ast::IncrementFixity::Postfix },
}

ArgumentExpressionList = { CommaSep<AssignmentExpression> }

UnaryOperator: ast::UnaryOp<'a> = {
    "&" => ast::UnaryOp { kind: ast::UnaryOpKind::Addressof, token: <> },
    "*" => ast::UnaryOp { kind: ast::UnaryOpKind::Deref, token: <> },
    "+" => ast::UnaryOp { kind: ast::UnaryOpKind::Plus, token: <> },
    "-" => ast::UnaryOp { kind: ast::UnaryOpKind::Negate, token: <> },
    "~" => ast::UnaryOp { kind: ast::UnaryOpKind::Compl, token: <> },
    "!" => ast::UnaryOp { kind: ast::UnaryOpKind::Not, token: <> },
}

UnaryExpression: ast::Expression<'a> = {
    PostfixExpression,
    <operator:UnaryOperator> <operand:Bump<CastExpression>> => ast::Expression::UnaryOp { <> },
    <sizeof:"sizeof"> <operand:Bump<UnaryExpression>> => ast::Expression::UnaryOp {
        operator: ast::UnaryOp { kind: ast::UnaryOpKind::Sizeof, token: sizeof },
        operand,
    },
    // TODO: `_Lengthof` basically duplicates all of the code for `sizeof`.
    <lengthof:"_Lengthof"> <operand:Bump<UnaryExpression>> => ast::Expression::UnaryOp {
        operator: ast::UnaryOp { kind: ast::UnaryOpKind::Lengthof, token: lengthof },
        operand,
    },
    <sizeof:"sizeof"> "(" <ty:TypeName> <close_paren:")"> => ast::Expression::Sizeof { <> },
    <lengthof:"_Lengthof"> "(" <ty:TypeName> <close_paren:")"> => ast::Expression::Lengthof { <> },
    <alignof:"alignof"> "(" <ty:TypeName> <close_paren:")"> => ast::Expression::Alignof { <> },
    <operator:IncrementOperator> <operand:Bump<UnaryExpression>> =>
        ast::Expression::Increment { <>, fixity: ast::IncrementFixity::Prefix },
}

CastExpression: ast::Expression<'a> = {
    UnaryExpression,
    <open_paren:"("> <ty:TypeName> ")" <expr:Bump<CastExpression>> => ast::Expression::Cast { <> },
}

MultiplicativeOperator: ast::BinOp<'a> = {
    "*" => ast::BinOp { kind: ast::BinOpKind::Multiply, token: <> },
    "/" => ast::BinOp { kind: ast::BinOpKind::Divide, token: <> },
    "%" => ast::BinOp { kind: ast::BinOpKind::Modulo, token: <> },
}

MultiplicativeExpression: ast::Expression<'a> = {
    CastExpression,
    <lhs:Bump<MultiplicativeExpression>> <op:MultiplicativeOperator> <rhs:Bump<CastExpression>> =>
        ast::Expression::BinOp { <> },
}

AdditiveOperator: ast::BinOp<'a> = {
    "+" => ast::BinOp { kind: ast::BinOpKind::Add, token: <> },
    "-" => ast::BinOp { kind: ast::BinOpKind::Subtract, token: <> },
}

AdditiveExpression: ast::Expression<'a> = {
    MultiplicativeExpression,
    <lhs:Bump<AdditiveExpression>> <op:AdditiveOperator> <rhs:Bump<MultiplicativeExpression>> =>
        ast::Expression::BinOp { <> },
}

ShiftOperator: ast::BinOp<'a> = {
    "<<" => ast::BinOp { kind: ast::BinOpKind::LeftShift, token: <> },
    ">>" => ast::BinOp { kind: ast::BinOpKind::RightShift, token: <> },
}

ShiftExpression: ast::Expression<'a> = {
    AdditiveExpression,
    <lhs:Bump<ShiftExpression>> <op:ShiftOperator> <rhs:Bump<AdditiveExpression>> =>
        ast::Expression::BinOp { <> },
}

RelationalOperator: ast::BinOp<'a> = {
    "<" => ast::BinOp { kind: ast::BinOpKind::Less, token: <> },
    "<=" => ast::BinOp { kind: ast::BinOpKind::LessEqual, token: <> },
    ">" => ast::BinOp { kind: ast::BinOpKind::Greater, token: <> },
    ">=" => ast::BinOp { kind: ast::BinOpKind::GreaterEqual, token: <> },
}

RelationalExpression: ast::Expression<'a> = {
    ShiftExpression,
    <lhs:Bump<RelationalExpression>> <op:RelationalOperator> <rhs:Bump<ShiftExpression>> =>
        ast::Expression::BinOp { <> },
}

EqualityOperator: ast::BinOp<'a> = {
    "==" => ast::BinOp { kind: ast::BinOpKind::Equal, token: <> },
    "!=" => ast::BinOp { kind: ast::BinOpKind::NotEqual, token: <> },
}

EqualityExpression: ast::Expression<'a> = {
    RelationalExpression,
    <lhs:Bump<EqualityExpression>> <op:EqualityOperator> <rhs:Bump<RelationalExpression>> =>
        ast::Expression::BinOp { <> },
}

BitAndOperator: ast::BinOp<'a> = {
    "&" => ast::BinOp { kind: ast::BinOpKind::BitAnd, token: <> },
}

BitAndExpression: ast::Expression<'a> = {
    EqualityExpression,
    <lhs:Bump<BitAndExpression>> <op:BitAndOperator> <rhs:Bump<EqualityExpression>> =>
        ast::Expression::BinOp { <> },
}

BitXorOperator: ast::BinOp<'a> = {
    "^" => ast::BinOp { kind: ast::BinOpKind::BitXor, token: <> },
}

BitXorExpression: ast::Expression<'a> = {
    BitAndExpression,
    <lhs:Bump<BitXorExpression>> <op:BitXorOperator> <rhs:Bump<BitAndExpression>> =>
        ast::Expression::BinOp { <> },
}

BitOrOperator: ast::BinOp<'a> = {
    "|" => ast::BinOp { kind: ast::BinOpKind::BitOr, token: <> },
}

BitOrExpression: ast::Expression<'a> = {
    BitXorExpression,
    <lhs:Bump<BitOrExpression>> <op:BitOrOperator> <rhs:Bump<BitXorExpression>> =>
        ast::Expression::BinOp { <> },
}

LogicalAndOperator: ast::LogicalOp<'a> = {
    "&&" => ast::LogicalOp { kind: ast::LogicalOpKind::And, token: <> },
}

LogicalAndExpression: ast::Expression<'a> = {
    BitOrExpression,
    <lhs:Bump<LogicalAndExpression>> <op:LogicalAndOperator> <rhs:Bump<BitOrExpression>> =>
        ast::Expression::Logical { <> },
}

LogicalOrOperator: ast::LogicalOp<'a> = {
    "||" => ast::LogicalOp { kind: ast::LogicalOpKind::Or, token: <> },
}

LogicalOrExpression: ast::Expression<'a> = {
    LogicalAndExpression,
    <lhs:Bump<LogicalOrExpression>> <op:LogicalOrOperator> <rhs:Bump<LogicalAndExpression>> =>
        ast::Expression::Logical { <> },
}

ConditionalExpression: ast::Expression<'a> = {
    LogicalOrExpression,
    <condition:Bump<LogicalOrExpression>>
        <question_mark:"?"> <then:Bump<Expression>>
        ":" <or_else:Bump<ConditionalExpression>> => ast::Expression::Conditional { <> },
}

CompoudAssignmentOperator: ast::BinOp<'a> = {
    "*=" => ast::BinOp { kind: ast::BinOpKind::Multiply, token: <> },
    "/=" => ast::BinOp { kind: ast::BinOpKind::Divide, token: <> },
    "%=" => ast::BinOp { kind: ast::BinOpKind::Modulo, token: <> },
    "+=" => ast::BinOp { kind: ast::BinOpKind::Add, token: <> },
    "-=" => ast::BinOp { kind: ast::BinOpKind::Subtract, token: <> },
    "<<=" => ast::BinOp { kind: ast::BinOpKind::LeftShift, token: <> },
    ">>=" => ast::BinOp { kind: ast::BinOpKind::RightShift, token: <> },
    "&=" => ast::BinOp { kind: ast::BinOpKind::BitAnd, token: <> },
    "^=" => ast::BinOp { kind: ast::BinOpKind::BitXor, token: <> },
    "|=" => ast::BinOp { kind: ast::BinOpKind::BitOr, token: <> },
}

AssignmentExpression: ast::Expression<'a> = {
    ConditionalExpression,
    <target:Bump<UnaryExpression>> "=" <value:Bump<AssignmentExpression>> => ast::Expression::Assign { <> },
    <target:Bump<UnaryExpression>> <op:CompoudAssignmentOperator> <value:Bump<AssignmentExpression>> =>
        ast::Expression::CompoundAssign { <> },
}

Expression: ast::Expression<'a> = {
    AssignmentExpression,
    <lhs:Bump<Expression>> "," <rhs:Bump<AssignmentExpression>> => ast::Expression::Comma { <> },
}

Bump<T>: &'a T = {
    T => sess.alloc(<>),
}

Some<T>: &'a [T] = {
    T+ => sess.alloc_slice_copy(&<>),
}

Many<T>: &'a [T] = {
    T* => sess.alloc_slice_copy(&<>),
}

CommaSep<T>: &'a [T] = {
    () => &[],
    NonEmptyCommaSep<T>,
}

CommaSepWithTrailingComma<T>: &'a [T] = {
    () => &[],
    <NonEmptyCommaSep<T>> ","?,
}

#[inline]
NonEmptyCommaSep<T>: &'a [T] = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        sess.alloc_slice_copy(&v)
    },
}
